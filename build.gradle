def applicationProperties = new Properties()
applicationProperties.load(new FileReader("$projectDir//src/main/resources/application.properties"))

configurations {
	liquibase
}

buildscript {
	ext {
		springBootVersion = '1.5.10.RELEASE'
	}
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0')
	}
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

docker {
	dependsOn build
	name project.name
	files jar.archivePath
	buildArgs(['JAR_FILE': "${jar.archiveName}"])
}

group = 'net.dlcruz'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile("org.springframework.boot:spring-boot-starter-thymeleaf")
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.codehaus.groovy:groovy')

	// Web jars
	compile('org.webjars:bootstrap:4.0.0-2')
	compile('org.webjars:font-awesome:5.0.8')

	// Security
	compile('org.springframework.security:spring-security-jwt:1.0.7.RELEASE')
	compile('org.springframework.security.oauth:spring-security-oauth2:2.1.0.RELEASE')

	compile('org.liquibase:liquibase-core')
	runtime('mysql:mysql-connector-java')

	// Facebook
	compile('org.springframework.social:spring-social-facebook:2.0.3.RELEASE')

	// Google
	compile ('org.springframework.social:spring-social-google:1.0.0.RELEASE')

	// Development
	compile("org.springframework.boot:spring-boot-devtools")

	// Liquibase Groovy DSL
	compile('org.liquibase:liquibase-groovy-dsl:1.2.2')

	// Test dependencies
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
	testCompile('org.spockframework:spock-spring:1.1-groovy-2.4')
	testRuntime('cglib:cglib-nodep:3.2.4')

	// Liquibase
	liquibase('org.liquibase:liquibase-core')
	liquibase('org.liquibase:liquibase-groovy-dsl:2.0.0')
	liquibase('mysql:mysql-connector-java:5.1.13')
	liquibase('org.liquibase.ext:liquibase-hibernate5:3.6')
	liquibase('org.springframework.boot:spring-boot-starter-data-jpa')
}

task diffChangeLog(type: JavaExec) {
	group = "liquibase"

	classpath sourceSets.main.runtimeClasspath
	classpath configurations.liquibase
	main = "liquibase.integration.commandline.Main"

	args "--changeLogFile=" + "$projectDir/src/main/resources/db/changelog/${System.currentTimeMillis()}-diff.groovy"
	args "--referenceUrl=" + "hibernate:spring:net.dlcruz.auth.model?dialect=org.hibernate.dialect.MySQL5Dialect&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy"
	args "--username=" + applicationProperties['spring.datasource.username']
	args "--password=" + applicationProperties['spring.datasource.password']
	args "--url=" + applicationProperties['spring.datasource.url']
	args "--referenceDriver=liquibase.ext.hibernate.database.connection.HibernateDriver"
	args "diffChangeLog"
}

task generateChangelog(type: JavaExec) {
	group = "liquibase"

	classpath sourceSets.main.runtimeClasspath
	classpath configurations.liquibase
	main = "liquibase.integration.commandline.Main"

	args "--changeLogFile=" + "$projectDir/src/main/resources/db/changelog/db.changelog-master.groovy"
	args "--url=" + "hibernate:spring:net.dlcruz.auth.model?dialect=org.hibernate.dialect.MySQL5Dialect&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy"
	args "--driver=liquibase.ext.hibernate.database.connection.HibernateDriver"
	args "generateChangelog"
}